buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
        springCloudVersion = 'Finchley.SR1'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE'
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
        classpath "com.netflix.nebula:nebula-release-plugin:6.0.2"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'
apply plugin: 'nebula.release'

group = 'com.github.bijukunjummen'
//version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("io.projectreactor:reactor-core")
    compile("com.netflix.hystrix:hystrix-core")
    testCompile('io.projectreactor:reactor-test')
    compile("io.reactivex:rxjava-reactive-streams")
    compile("org.slf4j:jcl-over-slf4j")
    testCompile("org.junit.jupiter:junit-jupiter-api")
    testRuntime("org.junit.jupiter:junit-jupiter-engine")
    testCompile("org.assertj:assertj-core")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

test {
    useJUnitPlatform()
}

bintray {
    user = bintray_user
    key = bintray_api_key
    configurations = ['archives']
    pkg {
        repo = 'repo'
        name = project.name
        group = project.group
//        version = project.version
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/bijukunjummen/reactor-hystrix.git'
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'reactor-hystrix'
            description 'A reactor wrapper over hystrix'
            url 'https://github.com/bijukunjummen/reactor-hystrix.git'
            inceptionYear '2018'

            packaging 'jar'
            groupId project.group
            artifactId project.name
            version project.version

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/bijukunjummen/reactor-hystrix.git'
                url 'https://github.com/bijukunjummen/reactor-hystrix.git'

            }
            developers {
                developer {
                    name 'Biju Kunjummen'
                }
            }
        }
    }
}